package rs.ac.bg.etf.pp1;

import java.io.*;
import java_cup.runtime.*;
import org.apache.log4j.*;
import rs.etf.pp1.mj.runtime.Code;
import rs.etf.pp1.symboltable.Tab;
import util.ImplementationClassHelper;
import rs.etf.pp1.symboltable.concepts.Obj;
import rs.etf.pp1.symboltable.concepts.Struct;

parser code {:
	
    int printCallCount = 0;

    Logger log = Logger.getLogger(getClass());
   
    // slede redefinisani metodi za prijavu gresaka radi izmene teksta poruke
     
    public void report_fatal_error(String message, Object info) throws java.lang.Exception {
      done_parsing();
      report_error(message, info);
    }
  
    public void syntax_error(Symbol cur_token) {
        report_error("\nSintaksna greska", cur_token);
    }
  
    public void unrecovered_syntax_error(Symbol cur_token) throws java.lang.Exception {
        report_fatal_error("Fatalna greska, parsiranje se ne moze nastaviti", cur_token);
    }

:}

action code {:
	ImplementationClassHelper parserHelper = new ImplementationClassHelper();
:}

init with {:
  Tab.init(); 
:}

scan with {:
	Symbol s = this.getScanner().next_token();
	if (s != null && s.value != null) 
		log.info(s.toString() + " " + s.value.toString());
	return s;
:}

terminal PROG, LBRACE, RBRACE, SEMI, LPAREN, RPAREN, COMMA, PRINT, RETURN, EQUAL, PLUS, MINUS, MUL, DIV, VOID, MOD, GREATER;
terminal IS_EQUAL, GREATER_EQUAL, IS_NOT_EQUAL, LOWER, LOWER_EQUAL, AND, OR, PLUS_EQUAL, MINUS_EQUAL, MUL_EQUAL, DIV_EQUAL;
terminal MOD_EQUAL, INC, DEC, DOT, LBRACKET, RBRACKET;
terminal Integer NUMBER;
terminal Character CHAR;
terminal Boolean BOOL;
terminal String IDENT;
terminal BREAK, CLASS, CONST, NEW, READ, FOR, EXTENDS, CONTINUE, STATIC, IF, ELSE;

non terminal Program, VarList, VarPars, VarDecl, MethodDecl, ConstList, ConstPars, ConstDecl, ClassDecl;
non terminal FormParamList, FormParams, ActualParams, ProgDeclLists, ProgLists, Extends, ConstType;
non terminal StatementList, Statement, LocalVarPars, LocalVarList, LocalVarDecl, LocalVarDeclList;
non terminal MethodRetType, MethodDeclList, DesignatorStat, MethodFormParams, ClassMethods;
non terminal ForDesiStatement, ForCondition, DesignatorStatTerm;
non terminal Obj ProgName, Designator, DesignatorTerm;
non terminal Struct Type, Term, Expr, TermList, Factor, Condition, CondFact, CondTerm;
non terminal Integer Relop, Mulop, Assignop, Addop, AddopLeft, AddopRight, MulopLeft, MulopRight;

precedence left ELSE;

Program ::=         PROG ProgName:prog ProgDeclLists LBRACE MethodDeclList RBRACE {: parserHelper.finalizeProgram(prog); :};
ProgName ::=        IDENT:pName {: RESULT = parserHelper.startProgram(pName); :};


ProgDeclLists ::=   ProgDeclLists ProgLists | /* empty */;
ProgLists ::=       ConstDecl | VarDecl | ClassDecl;

ConstDecl ::=       CONST Type:vType {: parserHelper.currentType = vType; :} ConstList SEMI;
ConstList ::=       ConstPars COMMA ConstList | ConstPars;
ConstType ::=       NUMBER:nName {: RESULT = nName; :} | 
                    BOOL:bName   {: RESULT = bName; :} | 
                    CHAR:cName   {: RESULT = cName; :} ;
ConstPars ::=       IDENT:cName EQUAL ConstType:cType {: parserHelper.insertConstVar(cName,cType,cNameleft); :} | 
                    IDENT:cName {: parserHelper.insertConstVar(cName,cNameleft); :} ;

VarDecl ::=         Type:vType {: parserHelper.currentType = vType; :} VarList SEMI |
                    error SEMI {: parserHelper.report_error("Uspesan oporavak od greske pri deklarisanju globalne promenljive"); :};
VarList ::=         VarPars COMMA VarList | VarPars;
VarPars ::=         IDENT:vName {: parserHelper.insertGlobalVar(vName,vNameleft); :} |
                    IDENT:vName LBRACKET RBRACKET {: parserHelper.insertGlobalArray(vName,vNameleft); :};


ClassDecl ::=       CLASS IDENT Extends LBRACE LocalVarDeclList ClassMethods RBRACE;
Extends ::=         EXTENDS Type | /* empty */;
ClassMethods ::=    LBRACE MethodDeclList RBRACE | /* empty */;


LocalVarDecl ::=    Type:vType {: parserHelper.currentType = vType; :} LocalVarList SEMI |
                    error SEMI {: parserHelper.report_error("Uspesan oporavak od greske pri deklarisanju lokalne promenljive"); :};
LocalVarList ::=    LocalVarPars COMMA LocalVarList | LocalVarPars;
LocalVarPars::=     IDENT:vName {: parserHelper.insertLocalVar(vName,vNameleft); :} |
                    IDENT:vName LBRACKET RBRACKET {: parserHelper.insertLocalArray(vName,vNameleft); :}; 
LocalVarDeclList::= LocalVarDeclList LocalVarDecl | /* empty */;


MethodDecl ::=      MethodRetType LPAREN MethodFormParams RPAREN LocalVarDeclList {: parserHelper.startMethod(); :} LBRACE StatementList RBRACE {: parserHelper.finalizeMethod(); :};
MethodRetType ::=   Type:mType IDENT:mName         {: parserHelper.insertMethod(mType,mName,mNameleft); parserHelper.globalFuncCount++; :} | 
                    STATIC Type:mType IDENT:mName  {: parserHelper.insertMethod(mType,mName,mNameleft); parserHelper.staticFuncCount++; :} | 
                    VOID IDENT:mName               {: parserHelper.insertMethod(null,mName,mNameleft);  parserHelper.globalFuncCount++; :} | 
                    STATIC VOID IDENT:mName        {: parserHelper.insertMethod(null,mName,mNameleft);  parserHelper.staticFuncCount++; :};
MethodDeclList ::=  MethodDeclList MethodDecl | /* empty */;
MethodFormParams::= FormParamList | /* empty */;

FormParamList ::=   FormParams COMMA FormParamList | FormParams;
FormParams ::=      Type:fType IDENT:vName | Type:fType IDENT:vName LBRACKET RBRACKET;

StatementList ::=   StatementList Statement | /* empty */;
Statement ::=       DesignatorStat SEMI |
                    error SEMI {: parserHelper.report_error("Uspesan oporavak od greske pri dodeli vrednosti promenljivoj"); :} |
                    IF LPAREN Condition:cName RPAREN Statement {: parserHelper.isBoolType(cName,cNameleft); :} |
                    IF LPAREN Condition:cName RPAREN Statement ELSE Statement {: parserHelper.isBoolType(cName,cNameleft); :} |
                    FOR LPAREN ForDesiStatement SEMI ForCondition SEMI ForDesiStatement RPAREN Statement |
                    BREAK SEMI | 
                    CONTINUE SEMI |
                    RETURN Expr:eType SEMI {: parserHelper.returnMatched(eType,eTypeleft); :} |
                    RETURN:rType SEMI      {: parserHelper.returnMatched(null,rTypeleft); :} |
                    READ LPAREN Designator:dName RPAREN SEMI {: parserHelper.read(dName,dNameleft); :} |
                    PRINT LPAREN Expr:eName COMMA NUMBER:len RPAREN SEMI {: parser.printCallCount++; parserHelper.print(eName,len,eNameleft); :} |
                    PRINT LPAREN Expr:eName RPAREN SEMI {: parser.printCallCount++; parserHelper.print(eName,eNameleft); :} |
                    LBRACE StatementList RBRACE;


ForDesiStatement ::= DesignatorStat | /* empty */;
ForCondition ::=    Condition |  /* empty */;

ActualParams ::=    Expr COMMA ActualParams | Expr;

Expr ::=            TermList:tName       {: RESULT = parserHelper.resolveType(tName,1); :} |
                    MINUS TermList:tName {: RESULT = parserHelper.resolveType(tName,0); :} ;
TermList ::=        Term:tName    {: RESULT = parserHelper.loadAgain(tName,0); :} |
                    Term:tName   Addop:add {: parserHelper.loadAgain(tName,add); :} TermList:tList {:  RESULT = parserHelper.executeAddop(tName,add,tList, tNameleft); :} ;
Term ::=            Factor:fName  {: RESULT = fName; :} |
                    Factor:fName Mulop:mul {: parserHelper.loadAgain(fName,mul); :} Term:tName {: parserHelper.loadAgain(tName,0); RESULT = parserHelper.executeMulop(fName,mul,tName, fNameleft); :} ;
 
Factor ::=          Designator:dName {: RESULT = parserHelper.pushDesignatorToStack(dName); :} | 
                    Designator:dName LPAREN RPAREN |
                    Designator:dName LPAREN ActualParams RPAREN |
                    NUMBER:nName {: RESULT = parserHelper.insertNumber(nName,nNameleft); :} |
                    CHAR:cName {: RESULT = parserHelper.insertChar(cName,cNameleft); :} |
                    BOOL:bName {: RESULT = parserHelper.insertBool(bName,bNameleft); :} |
                    NEW Type | 
                    NEW Type:tType LBRACKET Expr:eType {: RESULT = parserHelper.insertArray(tType, eType, eTypeleft); :} RBRACKET | 
                    LPAREN Expr:exp {: RESULT = parserHelper.insertExpr(exp,expleft); :} RPAREN;


Type ::=            IDENT:tName {: RESULT = parserHelper.findType(tName,tNameleft); :};
Designator ::=      IDENT:iName {: parserHelper.resolveIdent(iName,iNameleft); :} DesignatorTerm:dName {: RESULT = parserHelper.resolveIdent(iName,dName,dNameleft); :};

DesignatorTerm ::=  DOT IDENT DesignatorTerm | 
                    LBRACKET:lb {: RESULT = parserHelper.resolveDesignatorArray(lbleft); :} Expr:eName {: parserHelper.isIntType(eName, eNameleft); :} RBRACKET DesignatorTerm | /* empty */;

DesignatorStat ::=  Designator:dName {: parserHelper.setFirstDesignator(dName); :} DesignatorStatTerm ;

DesignatorStatTerm ::=  Assignop:aName {: parserHelper.resolveDesignator(aName); parserHelper.inAssign = true; :} Expr:eName {: parserHelper.executeAssignop(eName,aName,eNameleft); :} | 
                        LPAREN:r RPAREN {: parserHelper.callFunc(rleft); :} | 
                        LPAREN ActualParams:pName RPAREN {: parserHelper.callFunc(pNameleft); :} | 
                        INC:inc {: parserHelper.executeIncrement(incleft); :} | 
                        DEC:dec {: parserHelper.executeDecrement(decleft); :} ;


Condition ::=       CondTerm:cName {: RESULT = cName; :} OR  Condition | 
                    CondTerm:cName {: RESULT = cName; :} ;
CondTerm ::=        CondFact:cName {: RESULT = cName; :} AND  CondTerm | 
                    CondFact:cName {: RESULT = cName; :};
CondFact ::=        Expr:eName1 Relop:rName Expr:eName2 {: RESULT = parserHelper.executeBoolOperation(eName1,rName,eName2,rNameleft); :} | 
                    Expr:eName {: RESULT = eName; :};

Mulop ::=           MulopLeft:mulLeft {: RESULT = mulLeft; :} | MulopRight:mulRight {: RESULT = mulRight; :};
MulopLeft ::=       MUL {: RESULT = new Integer(Code.mul); :} | DIV {: RESULT = new Integer(Code.div); :} | MOD {: RESULT = new Integer(Code.rem); :};
MulopRight ::=      MUL_EQUAL {: RESULT = new Integer(parserHelper.opoffset + Code.mul); :} | DIV_EQUAL {: RESULT = new Integer(parserHelper.opoffset + Code.div); :} | MOD_EQUAL {: RESULT = new Integer(parserHelper.opoffset + Code.rem); :};

Addop ::=           AddopLeft:addLeft {: RESULT = addLeft; :} | AddopRight:addRight {: RESULT = addRight; :};
AddopLeft ::=       PLUS {: RESULT = new Integer(Code.add); :} | MINUS {: RESULT = new Integer(Code.sub); :};
AddopRight ::=      PLUS_EQUAL {: RESULT = new Integer(parserHelper.opoffset + Code.add); :} | MINUS_EQUAL {: RESULT = new Integer(parserHelper.opoffset + Code.sub); :};

Assignop ::=        EQUAL {: RESULT = parserHelper.opoffset; :} | Mulop:mul {: RESULT = mul; :} | Addop:add {: RESULT = add; :} ;
Relop ::=           IS_EQUAL {: RESULT = new Integer(Code.eq); :} | IS_NOT_EQUAL {: RESULT = new Integer(Code.ne); :} | GREATER {: RESULT = new Integer(Code.gt); :} | GREATER_EQUAL {: RESULT = new Integer(Code.ge); :} | LOWER {: RESULT = new Integer(Code.lt); :} | LOWER_EQUAL {: RESULT = new Integer(Code.le); :};